/* GRF allocation:
   g1~g30: constant buffer
           g1~g2:intra IQ matrix
           g3~g4:non intra IQ matrix
           g5~g20:IDCT table
   g31:    thread payload 
   g32:    message descriptor for reading reference data
   g58~g81:reference data
   g82:    thread payload backup
   g83~g106:IDCT data                           */
mov (8) g82.0<1>UD g31.0<8,8,1>UD {align1};
mov (1) g126.8<1>UD ip {align1};
add (1) ip g21.0<1,1,1>UD 0x50UD {align1};   //jump to the lib to do IDCT  

//Y, (x', y') = (x, y) + (motion_vector.x >> 1, motion_vector.y >> 1) 
asr (2) g31.14<1>W g82.14<2,2,1>W 1W {align1};  
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g32.16<1>UW 0UW {align1};           //0:forward 1:backward
mov (1) a0.0<1>UD 0x0A4EUD {align1};        //g82.14,motion vector
mov (1) g126.8<1>UD ip {align1};
add (1) ip g21.0<1,1,1>UD 0x00UD {align1};   //jump to the lib to read reference data  

//UV, (x', y') = (x >> 1, y >> 1) + (motion_vector.x >> 2, motion_vector.y >> 2)
shr (2) g31.0<1>UD g31.0<2,2,1>UD 1UD {align1};
asr (2) g31.14<1>W g82.14<2,2,1>W 2W {align1};
add (2) g32.0<1>UD g31.0<2,2,1>UD g31.14<2,2,1>W {align1};
mov (1) g126.8<1>UD ip {align1};
add (1) ip g21.0<1,1,1>UD 0x10UD {align1};          //jump to the lib to read reference data  

add (1) ip g21.0<1,1,1>UD 0x40UD {align1};          //jump to the lib to add the reference and idct data
